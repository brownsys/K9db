load("@rules_rust//rust:defs.bzl", "rust_binary", "rust_library")

config_setting(
    name = "asan",
    values = {"copt": "-DPELTON_ASAN"},
)

config_setting(
    name = "tsan",
    values = {"copt": "-DPELTON_TSAN"},
)

rust_binary(
    name = "proxy",
    srcs = [
        "src/proxy.rs",
    ],
    rustc_flags = [
        "-Clink-args=-L/home/pelton/bazel-bin/external/rocksdb/copy_rocksdb/rocksdb/lib",
        "-Clink-args=-L/home/pelton/pelton/bazel-bin/external/rocksdb/copy_rocksdb/rocksdb/lib",
        "-Clink-args=-L/home/bab/Documents/research/pelton/bazel-bin/external/rocksdb/copy_rocksdb/rocksdb/lib",
        "-Clink-args=-rdynamic",
        "-Clink-args=-Wl,-export-dynamic",
    ] + select({
        ":asan": [
            "-Clink-args=-fsanitize=address",
            "-Zsanitizer=address",
        ],
        ":tsan": [
            "-Clink-args=-fsanitize=thread",
            "-Zsanitizer=thread",
        ],
        "//conditions:default": [],
    }),
    visibility = ["//visibility:public"],
    deps = [
        ":ffi",
        "//pelton/proxy/cargo:ctrlc",
        "//pelton/proxy/cargo:gflags",
        "//pelton/proxy/cargo:lazy_static",
        "//pelton/proxy/cargo:msql_srv",
        "//pelton/proxy/cargo:regex",
        "//pelton/proxy/cargo:slog",
        "//pelton/proxy/cargo:slog_term",
        "//pelton/proxy/src/ffi",
    ],
)

# Includes both the bindgen generated ffi definitions and our wrappers.
# Also, depends on the ffi .so library so that it gets linked
# with our rust code.
rust_library(
    name = "ffi",
    srcs = [
        ":ffi_bindgen",
        ":ffi_wrappers",
    ],
    deps = [
        "//pelton/proxy/cargo:msql_srv",
    ],
)

# Invoke bindgen to generate rust definitions for the ffi library.
genrule(
    name = "ffi_bindgen",
    srcs = [
        "//pelton/proxy/src/ffi:ffi.h",
    ],
    outs = [
        "ffi_bindgen.rs",
    ],
    cmd = """
        ./$(location @raze__bindgen__0_32_3//:cargo_bin_bindgen) $< -o $@
    """,
    tools = [
        "@raze__bindgen__0_32_3//:cargo_bin_bindgen",
    ],
)

# Copy proxy_wrappers to bazel out / working directory
# This way, it becomes located in the exact same directory that
# contains proxy_ffi_bindgen.rs, and can thus include! it.
genrule(
    name = "ffi_wrappers",
    srcs = [
        "src/ffi.rs",
    ],
    outs = [
        "ffi.rs",
    ],
    cmd = """
        cp $< $@
    """,
)
