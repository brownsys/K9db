load("@rules_rust//rust:rust.bzl", "rust_binary", "rust_library")

config_setting(
    name = "asan",
    values = {"copt": "-DPELTON_ASAN"},
)

config_setting(
    name = "tsan",
    values = {"copt": "-DPELTON_TSAN"},
)

rust_binary(
    name = "mysql_proxy",
    srcs = [
        "main.rs",
    ],
    rustc_flags = [
        "-Clink-args=-L/home/pelton/bazel-bin/external/rocksdb/copy_rocksdb/rocksdb/lib",
        "-Clink-args=-L/home/pelton/pelton/bazel-bin/external/rocksdb/copy_rocksdb/rocksdb/lib",
        "-Clink-args=-lssl",
        "-Clink-args=-lcrypto",
        "-Clink-args=-rdynamic",
        "-Clink-args=-Wl,-export-dynamic",
    ] + select({
        ":asan": [
            "-Clink-args=-fsanitize=address",
            "-Zsanitizer=address",
        ],
        ":tsan": [
            "-Clink-args=-fsanitize=thread",
            "-Zsanitizer=thread",
        ],
        "//conditions:default": [],
    }),
    visibility = ["//visibility:public"],
    deps = [
        ":proxy_ffi",
        "//mysql_proxy/cargo:ctrlc",
        "//mysql_proxy/cargo:gflags",
        "//mysql_proxy/cargo:lazy_static",
        "//mysql_proxy/cargo:msql_srv",
        "//mysql_proxy/cargo:regex",
        "//mysql_proxy/cargo:slog",
        "//mysql_proxy/cargo:slog_term",
        "//mysql_proxy/src/ffi",
    ],
)

# Includes both the bindgen generated ffi definitions and our wrappers.
# Also, depends on the ffi .so library so that it gets linked
# with our rust code.
rust_library(
    name = "proxy_ffi",
    srcs = [
        ":proxy_ffi_bindgen",
        ":proxy_ffi_wrappers",
    ],
    deps = [
        "//mysql_proxy/cargo:msql_srv",
    ],
)

# Invoke bindgen to generate rust definitions for the ffi library.
genrule(
    name = "proxy_ffi_bindgen",
    srcs = [
        "//mysql_proxy/src/ffi:ffi.h",
    ],
    outs = [
        "proxy_ffi_bindgen.rs",
    ],
    cmd = """
        ./$(location @raze__bindgen__0_32_3//:cargo_bin_bindgen) $< -o $@
    """,
    tools = [
        "@raze__bindgen__0_32_3//:cargo_bin_bindgen",
    ],
)

# Copy proxy_wrappers to bazel out / working directory
# This way, it becomes located in the exact same directory that
# contains proxy_ffi_bindgen.rs, and can thus include! it.
genrule(
    name = "proxy_ffi_wrappers",
    srcs = [
        "src/proxy_ffi_wrappers.rs",
    ],
    outs = [
        "proxy_ffi.rs",
    ],
    cmd = """
        cp $< $@
    """,
)
